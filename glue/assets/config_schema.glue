model GlueConfigSchema {
	/// Configuration for code generation (`glue gen [...]`)
	generation?: Generation

	model Generation {
		/// Mode for the watermark at the top of the generated files. Defaults to "short" if not specified.
		watermark?: Watermark

		/// Mode for generating the watermark comment at the top of generated files
		enum Watermark: 
			/// Includes full details including generation timestamp, Glue version, source file, etc.
			"full" |
			/// Includes minimal details such as the the timestamp and source file
			"short" |
			/// No watermark comment is included
			"none"

		/// Configurations for Python code generation (`glue gen python [...]`)
		python?: Python
		model Python {
			/// The Python modeling library to use. Defaults to "pydantic".
			data_model_library?: DataModelLibrary
			/// The Python modeling library
			enum DataModelLibrary:
				/// Pydantic (v2) BaseModel classes with Field() annotations
				"pydantic" |
				/// Standard library dataclasses
				"dataclasses" |
				/// msgspec Struct classes
				"msgspec"
			/// For pydantic: the full import path for the base model class. Defaults to `pydantic.BaseModel`.
			base_model?: string
		}
	}
}
