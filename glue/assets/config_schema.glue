model GlueConfigSchema {
	/// Global defaults applied to all generation entries
	global?: Global

	/// Per-generation configuration entries
	gen?: GenConfig[]

	model Global {
		/// Base directory for generated output (relative to the config file if not absolute)
		output_base_dir?: string
		/// Default code generation configuration
		config?: Generation
	}

	model GenConfig {
		/// Glob selector for input files
		files: string
		/// Output path template (supports {file_name} and {file_ext})
		output?: string
		/// Per-generation configuration overrides
		config_overrides?: Generation
	}

	model Generation {
		/// Whether to emit lint suppression comments in generated files. Defaults to true.
		lint_suppressions?: bool

		/// Configurations for TypeScript code generation (`glue gen typescript [...]`)
		typescript?: TypeScript
		model TypeScript {
			/// Whether to emit Zod schemas and use z.infer for types. Defaults to false.
			zod?: bool
		}

		/// Mode for the watermark at the top of the generated files. Defaults to "short" if not specified.
		watermark?: Watermark

		/// Mode for generating the watermark comment at the top of generated files
		enum Watermark: 
			/// Includes full details including generation timestamp, Glue version, source file, etc.
			"full" |
			/// Includes minimal details such as the the timestamp and source file
			"short" |
			/// No watermark comment is included
			"none"

		/// Configurations for Python code generation (`glue gen python [...]`)
		python?: Python
		model Python {
			/// The Python modeling library to use. Defaults to "pydantic".
			data_model_library?: DataModelLibrary
			/// The Python modeling library
			enum DataModelLibrary:
				/// Pydantic (v2) BaseModel classes with Field() annotations
				"pydantic" |
				/// Standard library dataclasses
				"dataclasses" |
				/// attrs classes
				"attrs" |
				/// msgspec Struct classes
				"msgspec"
			/// For pydantic: the full import path for the base model class. Defaults to `pydantic.BaseModel`.
			base_model?: string
		}

		/// Configurations for Rust code generation (`glue gen rust [...]`)
		rust?: Rust
		model Rust {
			/// Include serde_yaml support in generated output
			include_yaml?: bool
		}
	}
}
