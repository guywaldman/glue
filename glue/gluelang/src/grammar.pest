SPACE = _{ " " | "\t" }
WHITESPACE = _{ SPACE | NEWLINE }
COMMENT = _{ "//" ~ (!"/") ~ (!NEWLINE ~ ANY)* ~ NEWLINE }

doc_comment = @{ SPACE* ~ "///" ~ (!NEWLINE ~ ANY)* }
doc_block = @{ (NEWLINE? ~ doc_comment ~ NEWLINE)+ }

ident = @{ (ASCII_ALPHA ~ (ASCII_ALPHANUMERIC | "_")*) | (ASCII_DIGIT ~ (ASCII_DIGIT+ | "XX")) }


string_literal = { "\"" ~ string_literal_inner ~ "\"" }
string_literal_inner = @{ char* }
char = {
    !("\"" | "\\") ~ ANY
    | "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
    | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
}
true = { "true" }
false = { "false" }
bool_literal = { true | false }
const_expr = { string_literal | bool_literal | ASCII_DIGIT+ }

builtin_type = { "int" | "string" | "bool" }
type_atom = { (#type_atom = ((builtin_type | ident | anon_model) ~ (#modifiers = ("[]"? ~ "?"? )))) }
type = { type_atom ~ ("|" ~ type_atom)* }

decorator_named_arg = { ident ~ "=" ~ const_expr }
decorator_positional_arg = { const_expr }
decorator_args = _{
    decorator_positional_arg ~ ("," ~ decorator_positional_arg)* ~ ("," ~ decorator_named_arg)*
    | decorator_named_arg ~ ("," ~ decorator_named_arg)*
}
decorator = { "@" ~ (#decorator_name = ident) ~ ("(" ~ decorator_args? ~ ")")? }

field_name = { ident | string_literal }
field = { doc_block? ~ decorator* ~ (#field_name = field_name) ~ ":" ~ (#field_type = type) ~ ("=" ~ const_expr)? }

enum_variant = { doc_block? ~ decorator* ~ (#variant_name = string_literal) }
enum_variants = { enum_variant ~ ("|" ~ enum_variant)* }
enum = { doc_block? ~ decorator* ~ "enum" ~ (#enum_name = ident) ~ ":" ~ enum_variants }

model_body = { ((#top_level_field = field) | model | enum)* }
model = { doc_block? ~ decorator* ~ "model" ~ (#model_name = ident) ~ "{" ~ model_body ~ "}" }
anon_model = { "{" ~ model_body ~ "}" }

endpoint = { doc_block? ~ decorator* ~ "endpoint" ~ (#endpoint_name = ident) ~ "{" ~ model_body ~ "}" }

program = _{ SOI ~ ((model | endpoint)+) ~ EOI }